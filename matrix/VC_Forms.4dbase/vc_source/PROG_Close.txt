//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 10/08/12, 16:39:35
// ----------------------------------------------------
// Description:
//   Clean up after progress bar use is complete.
//
//   NOTE: I found that rapidly adding and removing a
//   single progress bar was problematic with the 4D
//   Progress feature. 4D Progress wants to create and
//   delete the whole window and sometimes it doesn't
//   (it's asynchronous, unfortunately). So your code
//   can end up thinking there's a progress window open
//   when there isn't and vice versa.
//
//   Each pass through VC_MON_ProcessChanges opens up
//   to 4 progress bars.  Rather than open and close
//   them serially (and have the above issue), I leave
//   them open and close all of them at the end of the
//   operation.
//
//   This has the side-benefit of being less intrusive
//   to the user because when the container progress
//   window opens it steals focus too.
//
//   The point is I no longer really use this method
//   (see PROG_CloseAll).
// --[HEADEREND]---------------------------------------
C_LONGINT($1;$progressBarID_l)

C_LONGINT($4DProgressBarID_l;$found_l)

$progressBarID_l:=$1// ID also happens to be position in the arrays.

$found_l:=Find in array(PROG_IDs_al;$progressBarID_l)

If ($found_l>-1)
$4DProgressBarID_l:=PROG_4DIDS_AL{$progressBarID_l}

If ($4DProgressBarID_l#0)
Progress QUIT ($4DProgressBarID_l)
End if 

// Reset the counter for detecting of the progress bar should be shown.
// Probably not needed since I'll just be deleting the array elements,
// but the feature is there.
PROG_Reset ($progressBarID_l)

DELETE FROM ARRAY(PROG_4DIDs_al;$progressBarID_l)
DELETE FROM ARRAY(PROG_Title_at;$progressBarID_l)
DELETE FROM ARRAY(PROG_Current_al;$progressBarID_l)
DELETE FROM ARRAY(PROG_Total_al;$progressBarID_l)
DELETE FROM ARRAY(PROG_Threshold_al;$progressBarID_l)
DELETE FROM ARRAY(PROG_Progress_ar;$progressBarID_l)
DELETE FROM ARRAY(PROG_Message_at;$progressBarID_l)
DELETE FROM ARRAY(PROG_StartTime_al;$progressBarID_l)
DELETE FROM ARRAY(PROG_IDs_al;$progressBarID_l)

// Need to deal with PROG_Displayed_f, set it back to false if all progress bars have been closed.
If (Size of array(PROG_4DIDS_AL)=0)
PROG_DISPLAYED_F:=False
End if 
End if 
