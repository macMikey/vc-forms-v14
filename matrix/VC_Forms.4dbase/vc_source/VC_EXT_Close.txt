//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 04/04/12, 13:34:52
// ----------------------------------------------------
// Description:
//   Close connection to external database, if it's
//   open and no longer needed.
//
//   VC_EXT_Open and VC_EXT_Close are meant to be
//   used as a pair, but I want to allow nested
//   connections (there are commands that require a
//   connection).  So I added a counter to track the
//   number of connections.  In this way the connection
//   is REALLY closed only after the last request is
//   made.
// --[HEADEREND]---------------------------------------
C_LONGINT($0;$error_l)

C_LONGINT(VC_EXT_NumConnections_l)

$error_l:=0

// Only close if there's really a connection open.
If (VC_EXT_NumConnections_l>0)

// Decrement the counter.
VC_EXT_NumConnections_l:=VC_EXT_NumConnections_l-1

// If the counter is 0, the connection is truly no
// longer needed, close it.
If (VC_EXT_NumConnections_l=0)

// Switch back to internal database.
UTIL_ERR_HandlerInstall 
Begin SQL
USE REMOTE DATABASE DEFAULT;
End SQL
$error_l:=UTIL_ERR_HandlerRemove 

If ($error_l#0)
UTIL_ERR_ErrorMethod_t:=Current method name
End if 

End if 

Else 
$error_l:=<>ERR_EXT_NotOpen_l
UTIL_ERR_ErrorMethod_t:=Current method name
End if 

$0:=$error_l
