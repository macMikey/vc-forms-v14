<?xml version="1.0" encoding="windows-1252" standalone="no" ?>
<!DOCTYPE macros SYSTEM "http://www.4d.com/dtd/2007/macros.dtd" >
<macros>
	<macro name="BLD: Compiled Component" type_ahead="true" in_menu="false" in_toolbar="false" version="2">
		<text>// --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 07/25/13, 15:50:53
  // ----------------------------------------------------
  // Description:
  //   Create compiled and interpreted components.
  //
  //   Note the macros are copied to the compiled component.
  // --[HEADEREND]---------------------------------------
C_TEXT($1;$projectFilePath_t)

C_TEXT($docRef_t;$elemRef_t)
C_TEXT($buildDestPath_t;$buildAppName_t)

C_TEXT($pathToComponent_t)
C_TEXT($versionFile_t;$sourceMacros_t)

C_BLOB($versionFile_b)

C_LONGINT($found_l)
ARRAY TEXT($components_at;0)
COMPONENT LIST($components_at)
$found_l:=Find in array($components_at;"BLD")

If ($found_l=0)
ALERT("The interpreted build component (\"BLD.4dbase\") is required in order to build.")
Else 

$projectFilePath_t:=Get 4D folder(Database Folder)+"Preferences"+Folder separator+"BuildApp"+Folder separator
$projectFilePath_t:=$projectFilePath_t+"BuildApp.xml"

If (Test path name($projectFilePath_t)#Is a document)
ALERT("A build project file is required in order to build.")

Else 

BUILD APPLICATION($projectFilePath_t)

If (OK=1)

  // Get build folder and app name...
$docRef_t:=DOM Parse XML source($projectFilePath_t)
$elemRef_t:=DOM Find XML element($docRef_t;"Preferences4D/BuildApp/BuildWinDestFolder")
DOM GET XML ELEMENT VALUE($elemRef_t;$buildDestPath_t)
$elemRef_t:=DOM Find XML element($docRef_t;"Preferences4D/BuildApp/BuildApplicationName")
DOM GET XML ELEMENT VALUE($elemRef_t;$buildAppName_t)
DOM CLOSE XML($docRef_t)

$pathToComponent_t:=$buildDestPath_t+"Components"+Folder separator+$buildAppName_t+".4dbase"+Folder separator

  // Copy macros to compiled component.
$sourceMacros_t:=Get 4D folder(Database Folder)+"Macros v2"+Folder separator
If (Test path name($sourceMacros_t)=Is a folder)
COPY DOCUMENT($sourceMacros_t;$pathToComponent_t)
End if 

  // Create versioning file in Resources folder of the new component.
  // You must do this AFTER the build because the stamp increases.
$versionFile_t:=$pathToComponent_t+"version.txt"
If ($found_l>0)
EXECUTE METHOD("BLD_GetVersionFile";$versionFile_b)
End if 
BLOB TO DOCUMENT($versionFile_t;$versionFile_b)

  // Build interpreted component.
If ($found_l>0)
EXECUTE METHOD("BLD_Build";*;"interpreted")
End if 

Else 
ALERT("Compiled Build failed!")
End if 

End if 

End if </text>
	</macro>
</macros>